title New Game
author Russell Matney

require_player_movement

========
OBJECTS
========

Player
Blue Red White Black
.000.
.010.
01210
33333
.3.3.

TrailBlock
Purple
.000.
0...0
0...0
0...0
.000.

TrailOld
Gray

Prev
Red

Platform
Green YellowGreen
.000.
01.10
0.1.0
01.10
.000.

PlatformUsed
Green YellowGreen
.....
.010.
.101.
.010.
.....

Target
Red Orange
..0..
.1.1.
0.1.0
.1.1.
..0..

Background
DarkBrown

=======
LEGEND
=======

. = Background
p = Player
o = Platform
t = Target

pt = Platform or Target
x = Player and PlatformUsed

=======
SOUNDS
=======

sfx0 50392900 (platform sound)

================
COLLISIONLAYERS
================

Background
Platform, PlatformUsed, Target
Player
TrailOld, TrailBlock
Prev

======
RULES
======

(prevent moving toward the last spot)
[ > Player | ... | TrailBlock ] -> [ Player | ... | TrailBlock ]

(prevent moving if we've reached the target)
[ > Player Target ] -> [ Player Target ]

(move to platform or target, leaving last spot)
[ > Player | ... | pt ] -> [ TrailBlock Prev | ... | Player pt ] sfx0

(mark platform used)
[ Player Platform ] -> [ Player PlatformUsed ]

(otherwise! prevent movement)
[ > Player ] -> [ Player ]

(remove previous blocker)
[ Prev | ... | TrailBlock ] -> [ Prev | ... | ]
[ Prev ] -> [ ]

==============
WINCONDITIONS
==============

No Platform
All Player on Target

=======
LEVELS
=======

message "Hit all the platforms"

message "The red/orange must be last"

.......
.xooot.
.......

...o..t
.x...o.
...o.o.


.o.o...
.x.....
.t.o.o.
.o...o.

message "Be careful with your choices (z to undo)"

o..o.o.
ox.o.ot
...o.o.
.......

message "Show me whatcha got!"

.o....o
.x.ooo.
.o.....
.t.oooo

oo..o.o
ox.ooo.
.o.....
.t.oooo

oo..o.o
ox.ooo.
.t.....
.o.oooo

message "Congrats! Part 1 complete!"

message "Bonus level"

xoooooo
ooooooo
ooooooo
oooooot

message "Now for something more 'on theme'"

....o.o
tx..o.o
....o.o
tx..o.o
.......

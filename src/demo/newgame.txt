title New Game
author Russell Matney

require_player_movement

========
OBJECTS
========

PlayerA
Blue Green White Black
.000.
.010.
01210
33333
.3.3.

PlayerB
Blue Red White Black
.000.
.010.
01210
33333
.3.3.

TrailBlockA
Purple Red
.000.
0...0
0.1.0
0...0
.000.

TrailBlockB
Purple Red
.000.
0...0
0.1.0
0...0
.000.

TrailOld
Gray

PrevA
Green

PrevB
Red

Platform
Green YellowGreen
..0..
.010.
01011
.010.
..1..

PlatformUsed
Green YellowGreen
.....
.000.
.010.
.000.
.....

Target
Red Orange
..0..
.101.
00100
.101.
..0..

Background
DarkBrown

=======
LEGEND
=======

. = Background
a = PlayerA
b = PlayerB
o = Platform
t = Target

Player = a or b
pt = Platform or Target
x = PlayerA and PlatformUsed
y = PlayerB and PlatformUsed

=======
SOUNDS
=======

sfx0 50392900 (platform sound)

================
COLLISIONLAYERS
================

Background
Platform, PlatformUsed, Target
Player
TrailOld, TrailBlockA, TrailBlockB
PrevA, PrevB

======
RULES
======

(prevent moving toward the last spot)
[ > PlayerA | ... | TrailBlockA ] -> [ PlayerA | ... | TrailBlockA ]
[ > PlayerB | ... | TrailBlockB ] -> [ PlayerB | ... | TrailBlockB ]

(prevent moving if we've reached the target)
[ > Player Target ] -> [ Player Target ]

(move to platform or target, leaving last spot)
[ > PlayerA | ... | pt ] -> [ TrailBlockA PrevA | ... | PlayerA pt ] sfx0
[ > PlayerB | ... | pt ] -> [ TrailBlockB PrevB | ... | PlayerB pt ] sfx0

(mark platform used)
[ Player Platform ] -> [ Player PlatformUsed ]

(otherwise! prevent movement)
[ > Player ] -> [ Player ]

(remove previous blocker)
[ PrevA | ... | TrailBlockA ] -> [ PrevA | ... | ]
[ PrevA ] -> [ ]
[ PrevB  | ... | TrailBlockB  ] -> [ PrevB  | ... | ]
[ PrevB  ] -> [ ]

==============
WINCONDITIONS
==============

No Platform
All Player on Target

=======
LEVELS
=======

message "Hit all the platforms"

message "The red/orange must be last"

.......
.xooot.
.......

...o..t
.x...o.
...o.o.

.o.o...
.x.....
.t.o.o.
.o...o.

message "Be careful with your choices (z to undo)"

o..o.o.
ox.o.ot
...o.o.
.......

message "Show me whatcha got!"

.o....o
.x.ooo.
.o.....
.t.oooo

oo..o.o
ox.ooo.
.o.....
.t.oooo

oo..o.o
ox.ooo.
.t.....
.o.oooo

message "Congrats! Part 1 complete!"

message "Bonus level"

xoooooo
ooooooo
ooooooo
oooooot

message "Now for some 'on theme' levels"

.......
.xooot.
.......
.yooot.
.......

....o.o
tx..o.o
....o.o
ty..o.o
.......

....o.o
tx..o.o
......t
oo.o.y.
oo.o..o
